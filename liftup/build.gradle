apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = rootProject.ext.bintray.group
version = rootProject.ext.product.versionName

android {
    compileSdkVersion product.compileSdkVersion

    defaultConfig {
        minSdkVersion product.minSdkVersion
        targetSdkVersion product.targetSdkVersion
        versionCode product.versionCode
        versionName product.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation "jp.s64.android.recyclerview.customitemanimators:core:${product.versionName}"

    implementation "com.android.support:recyclerview-v7:${versions.android_support}"

    testImplementation "junit:junit:${versions.junit}"

    androidTestImplementation "com.android.support.test:runner:${versions.android_test_runner}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${versions.espresso}"
}

Properties properties = localProperties()

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.api_key')

    configurations = ['archives']

    pkg {
        repo = rootProject.ext.bintray.repository
        name = 'android-recyclerview-customitemanimators-liftup'
        desc = ''
        websiteUrl = rootProject.ext.bintray.siteUrl
        issueTrackerUrl = rootProject.ext.bintray.issuesUrl
        vcsUrl = rootProject.ext.bintray.gitUrl
        licenses = rootProject.ext.bintray.licenses
        labels = rootProject.ext.bintray.labels
        publish = false
        publicDownloadNumbers = true
        userOrg = rootProject.ext.bintray.userOrg

        version {
            name = rootProject.ext.product.versionName
            desc = ''
            vcsTag = rootProject.ext.product.versionName
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId rootProject.ext.bintray.group
                artifactId = 'core'
                name = 'android-recyclerview-customitemanimators-liftup'
                description = ''
                url rootProject.ext.bintray.sitUrl

                licenses {
                    license {
                        name rootProject.ext.bintray.licenseName
                        url rootProject.ext.bintray.licenseUrl
                    }
                }

                developers {
                    developer {
                        id rootProject.ext.bintray.developerId
                        name rootProject.ext.bintray.developerName
                        email rootProject.ext.bintray.developerEmail
                    }
                }
                scm {
                    connection rootProject.ext.bintray.gitUrl
                    developerConnection rootProject.ext.bintray.gitUrl
                    url rootProject.ext.bintray.siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

def localProperties() {
    Properties ret = new Properties()
    ret.load(project.rootProject.file('local.properties').newDataInputStream())
    return ret
}
